import java.text.SimpleDateFormat

apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'

// Sources
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId = 'com.github.tdascoli'
            artifactId = 'Rx3Firebase'
            version = "$project.version"
            // Publish AAR
            from components.android
            // Publish sources
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode createVersionCode()
        versionName "$project.version"
    }
}

dependencies {
    // firebase
    implementation "com.google.firebase:firebase-iid:21.0.0"
    implementation 'com.google.firebase:firebase-auth:20.0.0'
    implementation 'com.google.firebase:firebase-database:19.5.1'
    implementation "com.google.firebase:firebase-storage:19.2.0"
    implementation "com.google.firebase:firebase-firestore:22.0.0"
    implementation "com.google.firebase:firebase-functions:19.1.0"
    implementation "com.google.firebase:firebase-config:20.0.0"

    // rx
    implementation 'io.reactivex.rxjava3:rxjava:3.0.4'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    testImplementation 'com.google.firebase:firebase-auth:20.0.0'
    testImplementation 'com.google.firebase:firebase-database:19.5.1'
    testImplementation "com.google.firebase:firebase-storage:19.2.0"
    testImplementation "com.google.firebase:firebase-firestore:22.0.0"
    testImplementation "com.google.firebase:firebase-functions:19.1.0"
    testImplementation "com.google.firebase:firebase-config:20.0.0"
    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.mockito:mockito-core:3.3.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'androidx.test:core:1.3.0'
}

def createVersionCode(){
    def df = new SimpleDateFormat("yyMMddHHmm")
    df.setTimeZone(TimeZone.getTimeZone("de_CH"))
    def versionCode = df.format(new Date()).toInteger()
    println "#### createVersionCode: " + versionCode + " ####"

    return versionCode
}